/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    nf-core/mag Nextflow base config file
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    A 'blank slate' config file, appropriate for general use on most high performance
    compute environments. Assumes that all software is installed and available on
    the PATH. Runs in `local` mode - all jobs will be run on the logged in environment.
----------------------------------------------------------------------------------------
*/

process {

    cpus          = { 1 * task.attempt }
    memory        = { 7.GB * task.attempt }
    time          = { 4.h * task.attempt }

    errorStrategy = { task.exitStatus in ((130..145) + 104 + 175) ? 'retry' : 'finish' }
    maxRetries    = 3
    maxErrors     = '-1'

    // Process-specific resource requirements
    // NOTE - Please try and reuse the labels below as much as possible.
    //        These labels are used and recognised by default in DSL2 files hosted on nf-core/modules.
    //        If possible, it would be nice to keep the same label naming convention when
    //        adding in your local modules too.
    // See https://www.nextflow.io/docs/latest/config.html#config-process-selectors
    withLabel: process_single {
        cpus   = { 1 }
        memory = { 6.GB * task.attempt }
        time   = { 4.h * task.attempt }
    }
    withLabel: process_low {
        cpus   = { 2 * task.attempt }
        memory = { 12.GB * task.attempt }
        time   = { 4.h * task.attempt }
    }
    withLabel: process_medium {
        cpus   = { 6 * task.attempt }
        memory = { 36.GB * task.attempt }
        time   = { 8.h * task.attempt }
    }
    withLabel: process_high {
        cpus   = { 12 * task.attempt }
        memory = { 72.GB * task.attempt }
        time   = { 16.h * task.attempt }
    }
    withLabel: process_long {
        time = { 20.h * task.attempt }
    }
    withLabel: process_high_memory {
        memory = { 200.GB * task.attempt }
    }
    withLabel: error_ignore {
        errorStrategy = 'ignore'
    }
    withLabel: error_retry {
        errorStrategy = 'retry'
        maxRetries    = 2
    }
    withLabel: process_gpu {
        ext.use_gpu = { workflow.profile.contains('gpu') }
        accelerator = { workflow.profile.contains('gpu') ? 1 : null }
    }

    withName: BOWTIE2_HOST_REMOVAL_BUILD {
        cpus   = { 10 * task.attempt }
        memory = { 20.GB * task.attempt }
        time   = { 4.h * task.attempt }
    }
    withName: BOWTIE2_HOST_REMOVAL_ALIGN {
        cpus   = { 10 * task.attempt }
        memory = { 10.GB * task.attempt }
        time   = { 6.h * task.attempt }
    }
    withName: BOWTIE2_PHIX_REMOVAL_ALIGN {
        cpus   = { 4 * task.attempt }
        memory = { 8.GB * task.attempt }
        time   = { 6.h * task.attempt }
    }
    withName: PORECHOP_PORECHOP {
        cpus   = { 4 * task.attempt }
        memory = { 30.GB * task.attempt }
        time   = { 4.h * task.attempt }
    }
    withName: NANOLYSE {
        cpus   = { 2 * task.attempt }
        memory = { 10.GB * task.attempt }
        time   = { 3.h * task.attempt }
    }
    //filtlong: exponential increase of memory and time with attempts
    withName: FILTLONG {
        cpus   = { 8 * task.attempt }
        memory = { 64.GB * (2 ** (task.attempt - 1)) }
        time   = { 24.h * (2 ** (task.attempt - 1)) }
    }
    withName: CATPACK_BINS {
        memory = { 120.GB * task.attempt }
    }
    withName: CATPACK_CONTIGS {
        memory = { 60.GB * task.attempt }
    }
    withName: GTDBTK_CLASSIFYWF {
        cpus   = { 10 * task.attempt }
        memory = { 128.GB * task.attempt }
        time   = { 12.h * task.attempt }
    }
    //MEGAHIT returns exit code 250 when running out of memory
    withName: MEGAHIT {
        cpus          = { params.megahit_fix_cpu_1 ? 1 : (8 * task.attempt) }
        memory        = { 40.GB * task.attempt }
        time          = { 16.h * task.attempt }
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175 + 250) ? 'retry' : 'finish' }
    }
    //SPAdes returns error(1) if it runs out of memory (and for other reasons as well...)!
    //exponential increase of memory and time with attempts, keep number of threads to enable reproducibility
    withName: METASPADES {
        cpus          = { params.spades_fix_cpus != -1 ? params.spades_fix_cpus : (10 * task.attempt) }
        memory        = { 64.GB * (2 ** (task.attempt - 1)) }
        time          = { 24.h * (2 ** (task.attempt - 1)) }
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175 + 21 + 12 + 1) ? 'retry' : 'finish' }
        maxRetries    = 5
    }
    withName: METASPADESHYBRID {
        cpus          = { params.spadeshybrid_fix_cpus != -1 ? params.spadeshybrid_fix_cpus : (10 * task.attempt) }
        memory        = { 64.GB * (2 ** (task.attempt - 1)) }
        time          = { 24.h * (2 ** (task.attempt - 1)) }
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175 + 21 + 12 + 1) ? 'retry' : 'finish' }
        maxRetries    = 5
    }

    withName: METAMDBG_ASM {
        cpus   = { 12 * task.attempt }
        memory = { 72.GB * (2 ** (task.attempt - 1)) }
        time   = { 24.h * (2 ** (task.attempt - 1)) }
    }

    withName: FLYE {
        cpus   = { 12 * task.attempt }
        memory = { 72.GB * (2 ** (task.attempt - 1)) }
        time   = { 24.h * (2 ** (task.attempt - 1)) }
    }

    //returns exit code 247 when running out of memory
    withName: BOWTIE2_ASSEMBLY_ALIGN {
        cpus          = { 2 * task.attempt }
        memory        = { 8.GB * task.attempt }
        time          = { 8.h * task.attempt }
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175 + 247) ? 'retry' : 'finish' }
    }
    withName: METABAT2_METABAT2 {
        cpus   = { 8 * task.attempt }
        memory = { 20.GB * task.attempt }
        time   = { 8.h * task.attempt }
    }
    withName: MAG_DEPTHS {
        memory = { 16.GB * task.attempt }
    }
    withName: MAG_DEPTHS_PLOT {
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175) ? 'retry' : task.exitStatus == 1 ? 'ignore' : 'finish' }
    }
    withName: BUSCO_BUSCO {
        cpus          = { 10 * task.attempt }
        memory        = { 12.GB * task.attempt }
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175) ? 'retry' : 'ignore' }
    }
    withName: MAXBIN2 {
        errorStrategy = { task.exitStatus in [1, 255] ? 'ignore' : 'retry' }
    }
    withName: COMEBIN_RUNCOMEBIN {
        errorStrategy = { task.exitStatus in [1, 255] ? 'ignore' : 'retry' }
    }
    withName: DASTOOL_DASTOOL {
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175) ? 'retry' : task.exitStatus == 1 ? 'ignore' : 'finish' }
    }
    //as per https://github.com/Ecogenomics/CheckM/wiki/Installation#system-requirements
    withName: CHECKM_LINEAGEWF {
        memory = 42.GB
    }
    //CheckM2 returns exit code 1 when Diamond doesn't find any hits
    withName: CHECKM2_PREDICT {
        errorStrategy = { task.exitStatus in ((130..145) + 104 + 175) ? 'retry' : task.exitStatus == 1 ? 'ignore' : 'finish' }
    }
}
